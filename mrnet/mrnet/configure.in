dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(src/Network.C)
AC_CONFIG_SRCDIR(src/Network.C)
AC_CONFIG_HEADER([src/config.h])

AC_CONFIG_AUX_DIR(conf)
AC_CONFIG_SUBDIRS(xplat)

abs_srcdir=`cd $srcdir && pwd`

dnl === COMPILERS AND FLAGS ===
CXXFLAGS="-D__STDC_LIMIT_MACROS -D__STDC_CONSTANT_MACROS"

dnl hacks to allow user to define CXX, CXXFLAGS, etc without using env vars
dnl I don't like it -- suggestions?

dnl *** C Compiler and Options ***
AC_ARG_WITH(libfl,
            [  --with-libfl                Link line for flex library],
            [FLEX_LIB="${withval}";], 
            [AC_CHECK_LIB(fl, [printf], FLEX_LIB="-lfl", [AC_MSG_ERROR(Can't find flex library libfl. Must specify with --with-libfl)])] )

AC_ARG_WITH(outputlevel,
            [  --with-outputlevel          output level (0=>off, *1=>error*, 2=>info, 3=>lo-debug, 4=>mid-debug, 5=>hi-debug)],
            [if test "${withval}" = "0" ; then
               OUTPUT_LEVEL=0;
             elif test "${withval}" = "1" ; then
               OUTPUT_LEVEL=1;
             elif test "${withval}" = "2" ; then
               OUTPUT_LEVEL=2;
             elif test "${withval}" = "3" ; then
               OUTPUT_LEVEL=3;
             elif test "${withval}" = "4" ; then
               OUTPUT_LEVEL=4;
             elif test "${withval}" = "5" ; then
               OUTPUT_LEVEL=5;
             else
               OUTPUT_LEVEL=1;
             fi],
            [OUTPUT_LEVEL=1])

dnl TODO: figure out dlopen()/shared object idiosyncracies when using
dnl non-GNU compilers
AC_PROG_CXX(xlC CC g++)

AC_C_BIGENDIAN

AC_ARG_ENABLE(verbosebuild,
    [  --enable-verbosebuild                Display commands during build (default off)],
    [ VERBOSEMAKE="" ],
    [ VERBOSEMAKE=".SILENT:"] )
AC_SUBST(VERBOSEMAKE)

AC_ARG_ENABLE(debug,
    [  --enable-debug                       Enable debug mode options (default off)],
    [ debug_mode="yes"; CXXFLAGS="$CXXFLAGS -DDEBUG" ],
    [ debug_mode="no" ])

AC_SUBST(VERBOSEMAKE)


dnl === SETTING MRNET CONFIGURATION DEFAULTS ===

dnl MRNET_ARCH is host returned by config.guess with
dnl all `-` and `.` characters replaced by `_` so that a
dnl -D MRNET_ARCH can be passed to the compiler
dnl without it complaining

AC_CANONICAL_HOST
changequote(<<, >>)dnl
MRNET_ARCH=`echo $host | sed 's/[-|.]/_/g'`
MRNET_OS=`echo $host_os | sed 's/[-0-9].*//'`
MRNET_OSVER=`echo $host_os | sed 's/[-|.]/_/g'`
MRNET_ROOT=`pwd`
MRNET_VERSION='`cat "$(srcdir)"/VERSION`'
changequote([, ])dnl

AC_DEFINE_UNQUOTED(COMMNODE_EXE, "$MRNET_ROOT/bin/$host/mrnet_commnode")
AC_DEFINE_UNQUOTED(OUTPUT_LEVEL, $OUTPUT_LEVEL)
AC_SUBST(MRNET_ARCH)
AC_SUBST(MRNET_OS)
AC_SUBST(MRNET_ROOT)
AC_SUBST(MRNET_VERSION)

case "$host_os" in
  *linux*)
    LIBS="$LIBS $FLEX_LIB -lm -lpthread -ldl"
    LDFLAGS="-rdynamic"
    os_release=`uname -r`
    case "$os_release" in
      2.4*)
        CXXFLAGS="$CXXFLAGS -Dos_linux=24"
        ;;
      2.6*)
        CXXFLAGS="$CXXFLAGS -Dos_linux=26"
        ;;
      *)
        CXXFLAGS="$CXXFLAGS -Dos_linux"
        ;;
    esac
    ;;
  *aix4*)
    LIBS="$LIBS $FLEX_LIB -lm -lc -lpthread -ldl"
    CXXFLAGS="$CXXFLAGS -Dos_aix=4 -pthread"
    LDFLAGS="-pthread"
    ;;
  *aix5*)
    LIBS="$LIBS $FLEX_LIB -lm -lc -lpthread -ldl"
    CXXFLAGS="$CXXFLAGS -Dos_aix=5 -pthread"
    LDFLAGS="-pthread"
    ;;
  *solaris2.8*)
    LIBS="$LIBS $FLEX_LIB -lrpcsvc -lsocket -lnsl -lm -lpthread -ldl"
    CXXFLAGS="$CXXFLAGS -Dos_solaris=8"
    ;;
  *solaris2.9*)
    LIBS="$LIBS $FLEX_LIB -lrpcsvc -lsocket -lnsl -lm -lpthread -ldl"
    CXXFLAGS="$CXXFLAGS -Dos_solaris=9"
    ;;
  *sunos*)
    LIBS="$LIBS $FLEX_LIB -lrpcsvc -lsocket -lnsl -lm -lpthread -ldl"
    CXXFLAGS="$CXXFLAGS -Dos_sunos"
    ;;
  *irix*)
    LIBS="$LIBS $FLEX_LIB -lm -lc -lpthread -ldl"
    CXXFLAGS="$CXXFLAGS -Dos_irix"
    ;;
  *)
    LIBS="$LIBS $FLEX_LIB -lm -lpthread"
    ;;
esac
AC_SUBST(LIBS)

PD_COMPILER_TYPE
if test "$COMPILER_TYPE" = "gnu" ; then
    C_AS_CPLUSPLUS="-x c++"

    SOFLAGS=" -fPIC -shared -rdynamic"

    if test "$debug_mode" = "yes" ; then
      CXXFLAGS="$CXXFLAGS -g"
    else
      CXXFLAGS="$CXXFLAGS -O3"
    fi

elif test "$COMPILER_TYPE" = "aix-native" ; then
    C_AS_CPLUSPLUS="-+"

    if test "$debug_mode" = "$yes" ; then
      CXXFLAGS="$CXXFLAGS"
    else
      CXXFLAGS="$CXXFLAGS -O2"
    fi
fi

AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(whether the C++ compiler works ($CXX $CXXFLAGS).)
AC_TRY_COMPILE(
  [#include <stdio.h>],
  [int x;],
  [AC_MSG_RESULT(yes)],
  [AC_MSG_ERROR(no)])

AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(SOFLAGS)
AC_SUBST(C_AS_CPLUSPLUS)

ARFLAGS="cr"
AC_SUBST(ARFLAGS)

dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar)
AC_SUBST(AR)
AC_PATH_PROG(FLEX, flex)
AC_SUBST(FLEX)
AC_PATH_PROG(BISON, bison)
AC_SUBST(BISON)
AC_PATH_PROG(SHELL, sh)
AC_SUBST(SHELL)
AC_PROG_INSTALL

AC_CHECK_SIZEOF(char)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(float)
AC_CHECK_SIZEOF(double)

AS_MKDIR_P([obj/${host}])
AS_MKDIR_P([lib/${host}])
AS_MKDIR_P([bin/${host}])
AS_MKDIR_P([depends])

AC_OUTPUT( Makefile:conf/Makefile.in )
